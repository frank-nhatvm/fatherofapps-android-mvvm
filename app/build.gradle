plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-parcelize'
    id 'dagger.hilt.android.plugin'
}

def apikeyPropertiesFile = rootProject.file("apikey.properties")
def apikeyProperties = new Properties()
apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))

android {
    buildFeatures{
        dataBinding = true
    }

    compileSdkVersion 32

    defaultConfig {
        applicationId "com.fatherofapps.androidbase"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "com.fatherofapps.androidbase.CustomTestRunner"
    }


    android {
        sourceSets {
            String sharedTestDir = 'src/sharedTest/java'
            test {
                java.srcDirs += sharedTestDir
            }
            androidTest {
                java.srcDirs += sharedTestDir
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    flavorDimensions "default"
    productFlavors {

        dev {
            buildConfigField("String", "BASE_URL", apikeyProperties['BASE_URL_DEV'])
            buildConfigField("String", "ACCESS_TOKEN", apikeyProperties['ACCESS_TOKEN_DEV'])
        }

        staging {
            buildConfigField("String", "BASE_URL", apikeyProperties['BASE_URL_STAGING'])
            buildConfigField("String", "ACCESS_TOKEN", apikeyProperties['ACCESS_TOKEN_STAGING'])
        }

        live {
            buildConfigField("String", "BASE_URL", apikeyProperties['BASE_URL_LIVE'])
            buildConfigField("String", "ACCESS_TOKEN", apikeyProperties['ACCESS_TOKEN_LIVE'])
        }

    }

}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'

    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.2'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.1'


    // constraint layout
    def constraintlayout_version = '2.1.3'
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"

    // view model scope
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'

    // retrofit
    def retrofit_version = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"

    // moshi
    def  moshi_version ='1.11.0'
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"

    // okhttpclient
    def ok_httpclient_version = '4.9.0'
    implementation "com.squareup.okhttp3:okhttp:$ok_httpclient_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$ok_httpclient_version"

    // glide library
    def glide_version = '4.12.0'
    implementation "com.github.bumptech.glide:glide:$glide_version"

    // hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt("com.google.dagger:hilt-android-compiler:$hilt_version")

    // coroutine
    def  coroutines_version = '1.5.2'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // viewpager 2
    implementation "androidx.viewpager2:viewpager2:1.0.0"

    implementation "androidx.activity:activity-ktx:1.4.0"

    //circle image
    implementation 'de.hdodenhof:circleimageview:3.1.0'

    def room_version = "2.4.2"
    implementation("androidx.room:room-runtime:$room_version")
    // Kotlin annotation processing tool (kapt)
    kapt("androidx.room:room-compiler:$room_version")
    // Kotlin Extensions and Coroutines support for Room
    implementation("androidx.room:room-ktx:$room_version")
    // room test helper
    testImplementation("androidx.room:room-testing:$room_version")

    def fragment_version = '1.4.1'
    def fragment_ktx_version = '1.3.6'
    // Once https://issuetracker.google.com/127986458 is fixed this can be testImplementation
    debugImplementation "androidx.fragment:fragment-testing:$fragment_version"
    implementation "androidx.fragment:fragment-ktx:$fragment_ktx_version"

    // dependencies for local unit test
    def junit_version = '4.13.2'
    testImplementation "junit:junit:$junit_version"
    testImplementation 'com.google.truth:truth:1.1.3'

    def mockito_version = '3.12.4'
    testImplementation "org.mockito:mockito-core:$mockito_version"
    testImplementation 'org.mockito:mockito-inline:2.25.0'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"

    testImplementation "androidx.test.ext:junit-ktx:1.1.3"
    testImplementation "org.robolectric:robolectric:4.5.1"
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    def androidx_test_core_version = '1.4.0'
    implementation "androidx.test:core:$androidx_test_core_version"

    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
    androidTestImplementation "junit:junit:$junit_version"
    androidTestImplementation 'com.google.truth:truth:1.1.3'
    androidTestImplementation "org.mockito:mockito-core:$mockito_version"
    androidTestImplementation "org.mockito:mockito-android:$mockito_version"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"
    androidTestImplementation "androidx.test.ext:junit-ktx:1.1.3"
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:$ok_httpclient_version"
    androidTestImplementation  "com.google.dagger:hilt-android-testing:$hilt_version"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hilt_version"


}